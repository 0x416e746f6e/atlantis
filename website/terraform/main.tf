// This project sets up a static website at https://www.runatlantis.io and a
// redirect from the naked domain runatlantis.io to https://www.runatlantis.io.
// We use S3 to host the site, ACM for the SSL cert and CloudFront to front it.
// The site is generated by Hugo (see website/src).

provider "aws" {
  region = "us-east-1"
}

terraform {
  backend "s3" {
    bucket = "lkysow-terraform-states"
    key    = "runatlantis/atlantis/website"
    region = "us-east-1"
  }
}

variable "domain_name" {
  default = "www.runatlantis.io"
}

variable "naked_domain_name" {
  default = "runatlantis.io"
}

// First, set up the regular domain: www.runatlantis.io

// We want AWS to host our zone so its nameservers can point to our CloudFront
// distribution.
resource "aws_route53_zone" "zone" {
  name = "runatlantis.io"
}

// This Route53 record will point at our CloudFront distribution.
resource "aws_route53_record" "www" {
  zone_id = "${aws_route53_zone.zone.zone_id}"
  name    = "www.runatlantis.io"
  type    = "A"

  alias = {
    name                   = "${module.www_distribution.domain_name}"
    zone_id                = "${module.www_distribution.hosted_zone_id}"
    evaluate_target_health = false
  }
}

// Use the AWS Certificate Manager to create an SSL cert for our domain.
// This resource won't be created until you receive the email verifying you
// own the domain and you click on the confirmation link.
resource "aws_acm_certificate" "certificate" {
  // We want a wildcard cert so we can host subdomains later.
  domain_name       = "*.${var.naked_domain_name}"
  validation_method = "EMAIL"

  // We also want the cert to be valid for the naked domain even though we'll be
  // redirecting to the www. domain immediately.
  subject_alternative_names = ["${var.naked_domain_name}"]
}

// Now we're going to create an S3 bucket to hold our static website.

// Create an S3 Bucket that holds the website data. CloudFront will pull the
// website from this bucket.
resource "aws_s3_bucket" "www_runatlantis_io" {
  bucket = "${var.domain_name}"
  acl    = "public-read"
  policy = "${data.template_file.www_s3_bucket_policy.rendered}"

  website {
    index_document = "index.html"
    error_document = "404.html"
  }
}

// This template allows us to de-duplicates the IAM policy we need to apply
// to our S3 bucket to allow it to be readable by the world (since we want
// everyone to be able to see our site).
data "template_file" "www_s3_bucket_policy" {
  template = "${file("s3_bucket_policy.json")}"

  vars {
    domain_name = "${var.domain_name}"
  }
}

// Finally we're ready to create our CloudFront distribution. I've moved this
// into a module because we need two of them (the second for the naked domain)
// and there's a lot of code that would have been duplicated.
module "www_distribution" {
  source = "./modules/cloudfront_distribution"

  // CloudFront will use our SSL cert.
  acm_certificate_arn = "${aws_acm_certificate.certificate.arn}"
  cnames              = ["${var.domain_name}"]

  // CloudFront uses the S3 bucket's "website endpoint" to pull the actual
  // content for our website.
  domain_name = "${aws_s3_bucket.www_runatlantis_io.website_endpoint}"

  origin_id = "runatlantis_s3_bucket"
}

// We've set up our www.runatlantis.io domain, but we also want people to be
// able to type runatlantis.io or https://runatlantis.io and get redirected
// to https://www.runatlantis.io.
// To do this, we need to set up an S3 bucket like before but have it just
// redirect to https://www.runatlantis.io. We then need to set up a CloudFront
// distribution to host that redirect.

resource "aws_s3_bucket" "runatlantis_io" {
  bucket = "${var.naked_domain_name}"
  acl    = "public-read"
  policy = "${data.template_file.naked_s3_bucket_policy.rendered}"

  website {
    // Note this redirect. Here's where the magic happens.
    redirect_all_requests_to = "https://www.runatlantis.io"
  }
}

data "template_file" "naked_s3_bucket_policy" {
  template = "${file("s3_bucket_policy.json")}"

  vars {
    domain_name = "${var.naked_domain_name}"
  }
}

module "naked_distribution" {
  source              = "./modules/cloudfront_distribution"
  acm_certificate_arn = "${aws_acm_certificate.certificate.arn}"
  cnames              = ["${var.naked_domain_name}"]
  domain_name         = "${aws_s3_bucket.runatlantis_io.website_endpoint}"
  origin_id           = "naked_runatlantis_s3_bucket"
}

resource "aws_route53_record" "naked" {
  zone_id = "${aws_route53_zone.zone.zone_id}"

  // Note the name is blank here.
  name = ""
  type = "A"

  alias = {
    name                   = "${module.naked_distribution.domain_name}"
    zone_id                = "${module.naked_distribution.hosted_zone_id}"
    evaluate_target_health = false
  }
}
